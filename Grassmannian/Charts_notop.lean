import Mathlib.Tactic
import Mathlib.LinearAlgebra.ProjectiveSpace.Basic
import Mathlib.Topology.Algebra.Module.Basic
import Grassmannian.Grassmannian 
import Mathlib.Topology.Algebra.Module.FiniteDimension
import Mathlib.Analysis.NormedSpace.FiniteDimension


open Classical
noncomputable section 


section NoTopology 

variable {r : â„•} {ð•œ E U : Type*} [DivisionRing ð•œ] [AddCommGroup E] [Module ð•œ E] [AddCommGroup U] [Module ð•œ U]

namespace Grassmannian

/- We define what will be charts on Grassmannian ð•œ E r. The charts are indexed by linear equivalences
Ï† : E â‰ƒâ‚—[ð•œ] U Ã— (Fin r â†’ ð•œ), the model of each chart is ((Fin r â†’ ð•œ) â†’â‚—[ð•œ] U). The source of the chart is
the set of r-dimensional subspaces W such that Ï†.2 induces an isomorphism W â‰ƒ (Fin r â†’ ð•œ), or equivalently
such that W intersects Ker Ï†.1 trivially; we call this the Goodset of Ï†.1; its image is all of the codomain.
To have a chart defined at each point, we will need to assume that there exists a linear equivalence between E and 
(Fin r â†’ ð•œ) Ã— U, but we do that later.-/

/- Definition of the sources and lemmas about it.-/



def Goodset (Ï† : E â†’â‚—[ð•œ] (Fin r â†’ ð•œ)) : Set (Grassmannian ð•œ E r) :=
{W : Grassmannian ð•œ E r | W.1 âŠ“ (LinearMap.ker Ï†) = âŠ¥}

lemma GoodsetPreimage_iff_equiv (Ï† : E â†’â‚—[ð•œ] (Fin r â†’ ð•œ)) (v : Fin r â†’ E)  :
LinearIndependent ð•œ (Ï† âˆ˜ v) â†” Function.Bijective (LinearMap.domRestrict Ï† 
(Submodule.span ð•œ (Set.range v))) := by 
  constructor 
  . intro hv 
    have hker : LinearMap.ker (LinearMap.domRestrict Ï† (Submodule.span ð•œ (Set.range v))) = âŠ¥ := by
      ext âŸ¨u, huâŸ©
      simp only [LinearMap.mem_ker, LinearMap.domRestrict_apply, Submodule.mem_bot]
      constructor 
      . intro h
        rw [â†(Basis.constr_range (Pi.basisFun ð•œ (Fin r)) â„¤ ), LinearMap.mem_range] at hu
        obtain âŸ¨a, haâŸ© := hu
        simp_rw [â†ha] at h 
        simp only [Basis.constr_apply_fintype, Pi.basisFun_equivFun, LinearEquiv.refl_apply, map_sum,
          map_smul] at h  
        rw [Fintype.linearIndependent_iff] at hv 
        simp only [Submodule.mk_eq_zero]
        rw [â†ha]
        simp only [Basis.constr_apply_fintype, Pi.basisFun_equivFun, LinearEquiv.refl_apply]
        apply Finset.sum_eq_zero
        intro i _ 
        rw [hv a h i]
        simp only [zero_smul]
      . exact fun h => by simp only [Submodule.mk_eq_zero] at h; simp only [h, map_zero]
    rw [LinearMap.ker_eq_bot] at hker
    change _ âˆ§ _ 
    rw [and_iff_right hker]    
    have hv' : LinearIndependent ð•œ v := LinearIndependent.of_comp _ hv
    have hdim := finrank_span_eq_card hv' 
    simp only [Fintype.card_fin] at hdim  
    letI : FiniteDimensional ð•œ (Submodule.span ð•œ (Set.range v)) := by
      apply FiniteDimensional.span_of_finite 
      apply Set.finite_range
    refine (LinearMap.injective_iff_surjective_of_finrank_eq_finrank ?_).mp hker 
    simp only [FiniteDimensional.finrank_fintype_fun_eq_card, Fintype.card_fin]
    exact hdim 
  . intro hbij
    have hv : LinearIndependent ð•œ v := by
      rw [linearIndependent_iff_card_eq_finrank_span]
      simp only [Fintype.card_fin]
      change r = FiniteDimensional.finrank ð•œ _ 
      rw [â†(LinearMap.finrank_range_of_inj hbij.1)]
      rw [LinearMap.range_eq_top.mpr hbij.2]
      simp only [finrank_top, FiniteDimensional.finrank_fintype_fun_eq_card, Fintype.card_fin]
    apply LinearIndependent.map hv
    rw [Submodule.disjoint_def]
    intro u huv huÏ†
    have h : âŸ¨u, huvâŸ© âˆˆ LinearMap.ker (LinearMap.domRestrict Ï† (Submodule.span ð•œ (Set.range v))) := by
      simp only [LinearMap.mem_ker, LinearMap.domRestrict_apply]
      exact huÏ†
    have hker : LinearMap.ker (LinearMap.domRestrict Ï† (Submodule.span ð•œ (Set.range v))) = âŠ¥ := by
      rw [LinearMap.ker_eq_bot]
      exact hbij.1
    rw [hker] at h
    simp only [Submodule.mem_bot, Submodule.mk_eq_zero] at h  
    exact h 

lemma GoodsetPreimage (Ï† : E â†’â‚—[ð•œ] Fin r â†’ ð•œ) {v : Fin r â†’ E} (hv : LinearIndependent ð•œ v) :
Grassmannian.mk ð•œ v hv âˆˆ Goodset Ï† â†” LinearIndependent ð•œ (Ï† âˆ˜ v) := by
  rw [Goodset]
  simp only [ge_iff_le, Set.mem_setOf_eq]
  rw [Grassmannian.mk_apply, â†disjoint_iff]
  exact âŸ¨fun h => LinearIndependent.map hv h, fun h => Submodule.range_ker_disjoint hâŸ©

lemma GoodsetPreimage_unit (Ï† : E â†’â‚—[ð•œ] (Fin r â†’ ð•œ)) (v : Fin r â†’ E)  :
LinearIndependent ð•œ (Ï† âˆ˜ v) â†” 
IsUnit (Ï†.comp (Basis.constr (Pi.basisFun ð•œ (Fin r)) â„¤  v)) := by
  rw [LinearMap.isUnit_iff_ker_eq_bot]
  have heq : Ï†.comp (Basis.constr (Pi.basisFun ð•œ (Fin r)) â„¤  v) = 
    (Finsupp.total (Fin r) (Fin r â†’ ð•œ) ð•œ (Ï† âˆ˜ v)).comp
    (Finsupp.linearEquivFunOnFinite ð•œ ð•œ (Fin r)).symm.toLinearMap := by
    apply LinearMap.ext 
    intro u
    simp only [LinearMap.coe_comp, Function.comp_apply, Basis.constr_apply_fintype, Pi.basisFun_equivFun,
      LinearEquiv.refl_apply, map_sum, map_smul, LinearEquiv.coe_coe]
    rw [Finsupp.total_eq_fintype_total_apply (S := â„¤) (R := ð•œ), Fintype.total_apply]
    rfl
  rw [heq, LinearMap.ker_eq_bot, LinearMap.coe_comp]
  rw [Function.Injective.of_comp_iff', â†LinearMap.ker_eq_bot]
  rfl
  apply LinearEquiv.bijective  


lemma Goodset_iff_equiv (Ï† : E â†’â‚—[ð•œ] (Fin r â†’ ð•œ)) (W : Grassmannian ð•œ E r) :
W âˆˆ Goodset Ï† â†” Function.Bijective (LinearMap.domRestrict Ï† W.1) := by
  rw [â†Grassmannian.mk_rep W, GoodsetPreimage, GoodsetPreimage_iff_equiv] 
  rfl



/- Definition of the charts.-/

def ChartAux (Ï† : E â†’â‚—[ð•œ] Fin r â†’ ð•œ) (W : Grassmannian ð•œ E r) : (Fin r â†’ ð•œ) â†’â‚—[ð•œ] W.1 := by 
  by_cases hW : W âˆˆ Goodset Ï†
  . rw [Goodset_iff_equiv] at hW 
    exact (LinearEquiv.ofBijective (Ï†.domRestrict W.1) hW).symm.toLinearMap 
  . exact 0 

def Chart (Ï† : E â‰ƒâ‚—[ð•œ] (Fin r â†’ ð•œ) Ã— U) (W : Grassmannian ð•œ E r) : ((Fin r â†’ ð•œ) â†’â‚—[ð•œ] U) := 
((LinearMap.snd ð•œ (Fin r â†’ ð•œ) U).comp Ï†.toLinearMap).comp ((Submodule.subtype W.1).comp 
      (ChartAux ((LinearMap.fst ð•œ (Fin r â†’ ð•œ) U).comp Ï†.toLinearMap) W))
  

/- We lift the chart to a map on (Fin r â†’ E).-/

variable (ð•œ)

def LinearMapToSpan (v : Fin r â†’ E) : (Fin r â†’ ð•œ) â†’â‚—[ð•œ] Submodule.span ð•œ (Set.range v) := by
  refine LinearMap.codRestrict (Submodule.span ð•œ (Set.range v)) (Basis.constr (Pi.basisFun ð•œ (Fin r)) â„¤  v) ?_
  rw [â†(Basis.constr_range (Pi.basisFun ð•œ (Fin r)) â„¤ )]
  simp only [Basis.constr_apply_fintype, Pi.basisFun_equivFun, LinearEquiv.refl_apply, LinearMap.mem_range,
    exists_apply_eq_apply, forall_const]

variable {ð•œ}

lemma LinearMapToSpan_vs_Basis.constr (v : Fin r â†’ E) :
(Basis.constr (Pi.basisFun ð•œ (Fin r)) â„¤  v) = LinearMap.comp
(Submodule.subtype (Submodule.span ð•œ (Set.range v))) (LinearMapToSpan ð•œ v) := by
  unfold LinearMapToSpan
  simp only [LinearMap.subtype_comp_codRestrict]

lemma LinearMapToSpan.bijective_iff (v : Fin r â†’ E) : 
Function.Bijective (LinearMapToSpan ð•œ v) â†” LinearIndependent ð•œ v := by
  have heq : Function.Bijective (LinearMapToSpan ð•œ v) â†” Function.Injective (LinearMapToSpan ð•œ v) := by
    change _ âˆ§_ â†” _ 
    simp only [and_iff_left_iff_imp] 
    intro _ 
    intro âŸ¨v, hvâŸ© 
    rw [â†(Basis.constr_range (Pi.basisFun ð•œ (Fin r)) â„¤)] at hv
    unfold LinearMapToSpan 
    simp only [LinearMap.mem_range] at hv
    obtain âŸ¨a, haâŸ© := hv
    existsi a 
    rw [â†SetCoe.ext_iff, LinearMap.codRestrict_apply]
    exact ha    
  rw [heq, â†LinearMap.ker_eq_bot]
  unfold LinearMapToSpan
  rw [LinearMap.ker_codRestrict, Fintype.linearIndependent_iff] 
  constructor 
  . intro h f hf
    have hker : f âˆˆ LinearMap.ker (Basis.constr (Pi.basisFun ð•œ (Fin r)) â„¤ v) := by
      simp only [LinearMap.mem_ker, Basis.constr_apply_fintype, Pi.basisFun_equivFun, LinearEquiv.refl_apply]
      exact hf 
    rw [h] at hker 
    simp only [Submodule.mem_bot] at hker  
    rw [hker]
    simp only [Pi.zero_apply, implies_true]
  . intro h 
    ext f 
    simp only [LinearMap.mem_ker, Basis.constr_apply_fintype, Pi.basisFun_equivFun, LinearEquiv.refl_apply,
      Submodule.mem_bot]
    constructor 
    . intro hker 
      ext i 
      simp only [Pi.zero_apply]
      exact h f hker i
    . exact fun h => by rw [h]; simp only [Pi.zero_apply, zero_smul, Finset.sum_const_zero]
    
    
def ChartAuxLift (Ï† : E â†’â‚—[ð•œ] Fin r â†’ ð•œ) (v : Fin r â†’ E) : 
(Fin r â†’ ð•œ) â†’â‚—[ð•œ] Submodule.span ð•œ (Set.range v) := by
  by_cases hgood : LinearIndependent ð•œ (Ï† âˆ˜ v) 
  . have hv : LinearIndependent ð•œ v := LinearIndependent.of_comp Ï† hgood 
    rw [â†(GoodsetPreimage (hv := hv)), Goodset_iff_equiv] at hgood
    have hbij : Function.Bijective (((LinearMap.domRestrict Ï† (Grassmannian.mk ð•œ v hv).1)).comp 
      (LinearMapToSpan ð•œ v)) := by
      rw [LinearMap.coe_comp]
      exact Function.Bijective.comp hgood ((LinearMapToSpan.bijective_iff v).mpr hv)
    exact (LinearMapToSpan ð•œ v).comp (LinearEquiv.ofBijective _ hbij).symm.toLinearMap  
  . exact 0 

lemma ChartAuxLift.isLift (Ï† : E â†’â‚—[ð•œ] Fin r â†’ ð•œ) {v : Fin r â†’ E} 
(hv : LinearIndependent ð•œ v) :
ChartAux Ï† (Grassmannian.mk ð•œ v hv) = ChartAuxLift Ï† v := by
  unfold ChartAux ChartAuxLift
  by_cases hgood : Grassmannian.mk ð•œ v hv âˆˆ Goodset Ï†
  . simp only [hgood, dite_true]
    rw [GoodsetPreimage] at hgood
    simp only [hgood, dite_true]
    apply LinearMap.coe_injective  
    erw [LinearMap.coe_comp (LinearMapToSpan ð•œ v)]
    erw [LinearEquiv.eq_comp_symm, LinearEquiv.symm_comp_eq]
    ext u 
    simp only [LinearEquiv.ofBijective_apply, LinearMap.coe_comp, Function.comp_apply, LinearMap.domRestrict_apply]
    rfl 
  . simp only [hgood, dite_false]
    rw [GoodsetPreimage] at hgood
    simp only [hgood, dite_false]


def ChartLiftAux (Ï† : E â‰ƒâ‚—[ð•œ] (Fin r â†’ ð•œ) Ã— U)  (v : Fin r â†’ E) : ((Fin r â†’ ð•œ) â†’â‚—[ð•œ] E) :=
(Basis.constr (Pi.basisFun ð•œ (Fin r)) â„¤  v).comp 
(Ring.inverse (LinearMap.comp ((LinearMap.fst ð•œ (Fin r â†’ ð•œ) U).comp Ï†.toLinearMap) 
(Basis.constr (Pi.basisFun ð•œ (Fin r)) â„¤  v)))


def ChartLift (Ï† : E â‰ƒâ‚—[ð•œ] (Fin r â†’ ð•œ) Ã— U)  (v : Fin r â†’ E) : ((Fin r â†’ ð•œ) â†’â‚—[ð•œ] U) := 
  ((LinearMap.snd ð•œ (Fin r â†’ ð•œ) U).comp Ï†.toLinearMap).comp 
  ((Basis.constr (Pi.basisFun ð•œ (Fin r)) â„¤  v).comp 
(Ring.inverse (LinearMap.comp ((LinearMap.fst ð•œ (Fin r â†’ ð•œ) U).comp Ï†.toLinearMap) 
(Basis.constr (Pi.basisFun ð•œ (Fin r)) â„¤  v))))

lemma ChartLift_vs_ChartAuxLift(Ï† : E â‰ƒâ‚—[ð•œ] (Fin r â†’ ð•œ) Ã— U)  (v : Fin r â†’ E) :
ChartLift Ï† v = ((LinearMap.snd ð•œ (Fin r â†’ ð•œ) U).comp Ï†.toLinearMap).comp
((Submodule.subtype (Submodule.span ð•œ (Set.range v))).comp 
(ChartAuxLift (((LinearMap.fst ð•œ (Fin r â†’ ð•œ) U).comp Ï†.toLinearMap)) v)) := by
  set Ï†â‚ := (LinearMap.comp (LinearMap.fst ð•œ (Fin r â†’ ð•œ) U) Ï†.toLinearMap) with h1def 
  unfold ChartLift
  apply congrArg 
  unfold ChartAuxLift 
  by_cases hgood : LinearIndependent ð•œ ((((LinearMap.fst ð•œ (Fin r â†’ ð•œ) U).comp Ï†.toLinearMap)) âˆ˜ v)
  . simp only [LinearMap.coe_comp, LinearEquiv.coe_coe] at hgood 
    simp only [LinearMap.coe_comp, LinearEquiv.coe_coe, hgood, dite_true]
    have hgood' := hgood
    erw [GoodsetPreimage_unit (((LinearMap.fst ð•œ (Fin r â†’ ð•œ) U).comp Ï†.toLinearMap))] at hgood 
    rw [LinearMapToSpan_vs_Basis.constr, LinearMap.comp_assoc]
    apply congrArg 
    have hinj : Function.Injective (LinearMap.comp (LinearMap.comp (LinearMap.fst ð•œ (Fin r â†’ ð•œ) U) Ï†.toLinearMap)
      (Submodule.subtype (Submodule.span ð•œ (Set.range v)))) := by
      erw [GoodsetPreimage_iff_equiv (((LinearMap.fst ð•œ (Fin r â†’ ð•œ) U).comp Ï†.toLinearMap))] at hgood' 
      exact hgood'.1 
    rw [â†(LinearMap.cancel_left hinj)]
    simp_rw [â†h1def]
    rw [LinearMap.comp_assoc, â†(LinearMap.comp_assoc _ (LinearMapToSpan ð•œ v)), â†LinearMapToSpan_vs_Basis.constr]
    change (LinearMap.comp Ï†â‚ ((Basis.constr (Pi.basisFun ð•œ (Fin r)) â„¤) v)) * 
              (Ring.inverse (LinearMap.comp Ï†â‚ ((Basis.constr (Pi.basisFun ð•œ (Fin r)) â„¤) v))) = _ 
    rw [Ring.mul_inverse_cancel _ hgood]
    rw [â†LinearMap.comp_assoc]
    apply LinearMap.coe_injective
    rw [LinearMap.coe_comp]
    erw [LinearEquiv.eq_comp_symm]
    ext u
    simp only [LinearMap.coe_comp, LinearEquiv.coe_coe, Function.comp_apply, LinearEquiv.ofBijective_apply,
      LinearMap.domRestrict_apply, LinearMap.fst_apply, LinearMap.one_apply, Submodule.coeSubtype]
    rfl
  . simp only [LinearMap.coe_comp, LinearEquiv.coe_coe] at hgood  
    simp only [LinearMap.coe_comp, LinearEquiv.coe_coe, hgood, dite_false, LinearMap.comp_zero]
    erw [GoodsetPreimage_unit (((LinearMap.fst ð•œ (Fin r â†’ ð•œ) U).comp Ï†.toLinearMap))] at hgood 
    rw [Ring.inverse_non_unit _ hgood] 
    simp only [LinearMap.comp_zero]


lemma ChartLift_isLift (Ï† : E â‰ƒâ‚—[ð•œ] (Fin r â†’ ð•œ) Ã— U) {v : Fin r â†’ E} (hv : LinearIndependent ð•œ v) :
Chart Ï† (Grassmannian.mk ð•œ v hv) = ChartLift Ï† v := by
  rw [ChartLift_vs_ChartAuxLift, â†ChartAuxLift.isLift]
  rfl 



/- Definition of the inverse chart.-/


def InverseChart (Ï† : E â‰ƒâ‚—[ð•œ] (Fin r â†’ ð•œ) Ã— U) : ((Fin r â†’ ð•œ) â†’â‚—[ð•œ] U) â†’ Grassmannian ð•œ E r := by 
  intro f 
  refine âŸ¨Submodule.map Ï†.symm (LinearMap.graph f), ?_âŸ©
  unfold Grassmannian
  simp only [Set.mem_setOf_eq]
  constructor
  . letI := LinearEquiv.finiteDimensional (LinearMap.graph_equiv_fst f).symm 
    apply Module.Finite.map 
  . erw [LinearEquiv.finrank_map_eq Ï†.symm]
    rw [LinearEquiv.finrank_eq (LinearMap.graph_equiv_fst f)]
    simp only [FiniteDimensional.finrank_fintype_fun_eq_card, Fintype.card_fin]

lemma InverseChart_codomainGoodset (Ï† : E â‰ƒâ‚—[ð•œ] (Fin r â†’ ð•œ) Ã— U) (f : (Fin r â†’ ð•œ) â†’â‚—[ð•œ] U) :
InverseChart Ï† f âˆˆ Goodset ((LinearMap.fst ð•œ (Fin r â†’ ð•œ) U).comp Ï†.toLinearMap) := by
  rw [Goodset_iff_equiv]
  unfold InverseChart
  simp only
  erw [LinearMap.coe_comp]
  simp only [LinearMap.coe_comp, LinearEquiv.coe_coe, Submodule.coeSubtype]
  rw [Set.bijective_iff_bijOn_univ]
  apply Set.BijOn.comp (t := Submodule.map (LinearEquiv.symm Ï†) (LinearMap.graph f))
  . apply Set.BijOn.comp (t := LinearMap.graph f)
    . apply Set.BijOn.mk
      . apply Set.mapsTo_univ
      . rw [Set.injOn_iff_injective]
        exact LinearMap.graph_fst_injective f         
      . rw [Set.surjOn_iff_surjective]
        exact LinearMap.graph_fst_surjective f                 
    . simp only [Submodule.map_coe]
      apply Equiv.bijOn' Ï†.toEquiv 
      . simp only [LinearEquiv.coe_toEquiv, Set.maps_image_to]
        intro u
        simp only [SetLike.mem_coe, LinearMap.mem_graph_iff, Function.comp_apply, LinearEquiv.apply_symm_apply,
          imp_self]
      . simp only [LinearEquiv.coe_toEquiv_symm]
        intro u
        simp only [SetLike.mem_coe, LinearMap.mem_graph_iff, Set.mem_image, Prod.exists, exists_eq_left]
        intro hu
        existsi u.1 
        rw [â†hu]
        simp only [Prod.mk.eta]
        rfl
  . constructor 
    . simp only [Submodule.map_coe, Set.maps_univ_to, Set.mem_image, SetLike.mem_coe, LinearMap.mem_graph_iff,
      Prod.exists, exists_eq_left, Subtype.forall, Submodule.mem_map, forall_exists_index, forall_apply_eq_imp_iff',
      EmbeddingLike.apply_eq_iff_eq, Prod.mk.injEq, forall_const]
    . rw [and_iff_right Set.injOn_subtype_val]
      have heq : Submodule.map (LinearEquiv.symm Ï†) (LinearMap.graph f) = (fun (x : Submodule.map 
        (LinearEquiv.symm Ï†) (LinearMap.graph f)) => x.1) '' Set.univ := by
        simp only [Submodule.map_coe, Set.image_univ, Subtype.range_coe_subtype, Submodule.mem_map,
          LinearMap.mem_graph_iff, Prod.exists, exists_eq_left]
        ext u
        simp only [Set.mem_image, SetLike.mem_coe, LinearMap.mem_graph_iff, Prod.exists, exists_eq_left,
          Set.mem_setOf_eq]
      rw [heq]
      apply Set.surjOn_image 

def InverseChartLift (Ï† : E â‰ƒâ‚—[ð•œ] (Fin r â†’ ð•œ) Ã— U) (f : (Fin r â†’ ð•œ) â†’â‚—[ð•œ] U) : Fin r â†’ E :=
Ï†.symm âˆ˜ (LinearMap.prod LinearMap.id f) âˆ˜ (fun i => (Pi.basisFun ð•œ (Fin r)) i)

lemma InverseChartLift_codomain (Ï† : E â‰ƒâ‚—[ð•œ] (Fin r â†’ ð•œ) Ã— U) (f : (Fin r â†’ ð•œ) â†’â‚—[ð•œ] U) :
LinearIndependent ð•œ (((LinearMap.fst ð•œ (Fin r â†’ ð•œ) U).comp Ï†.toLinearMap) âˆ˜ (InverseChartLift Ï† f)) := by
  unfold InverseChartLift
  simp only [LinearMap.coe_comp, LinearEquiv.coe_coe]
  rw [â†Function.comp.assoc, Function.comp.assoc _ Ï† _]
  rw [Fintype.linearIndependent_iff]
  intro a
  simp only [Function.comp_apply, LinearEquiv.apply_symm_apply, LinearMap.fst_apply]
  intro ha i 
  apply_fun (fun h => h i) at ha 
  simp only [Finset.sum_apply, Pi.smul_apply, ne_eq, smul_eq_mul, Pi.zero_apply] at ha  
  rw [â†(Finset.sum_subset (sâ‚ := {i}))] at ha 
  . simp only [Pi.basisFun_apply, LinearMap.prod_apply, Pi.prod, LinearMap.id_coe, id_eq, LinearMap.stdBasis_apply',
    mul_ite, mul_one, mul_zero, Finset.sum_ite_eq', Finset.mem_singleton, ite_true] at ha  
    exact ha 
  . simp only [Finset.subset_univ]
  . intro j _ hj 
    simp only [Finset.mem_singleton] at hj 
    simp only [Pi.basisFun_apply, LinearMap.prod_apply, Pi.prod, LinearMap.id_coe, id_eq, LinearMap.stdBasis_apply',
      mul_ite, mul_one, mul_zero, ite_eq_right_iff]
    simp only [hj, IsEmpty.forall_iff]

lemma InverseChartLift_codomain' (Ï† : E â‰ƒâ‚—[ð•œ] (Fin r â†’ ð•œ) Ã— U) (f : (Fin r â†’ ð•œ) â†’â‚—[ð•œ] U) :
LinearIndependent ð•œ (InverseChartLift Ï† f) := 
LinearIndependent.of_comp _ (InverseChartLift_codomain Ï† f)

lemma InverseChartLift_isLift (Ï† : E â‰ƒâ‚—[ð•œ] (Fin r â†’ ð•œ) Ã— U) (f : (Fin r â†’ ð•œ) â†’â‚—[ð•œ] U) :
InverseChart Ï† f = Grassmannian.mk ð•œ (InverseChartLift Ï† f) (InverseChartLift_codomain' Ï† f) := by
  unfold InverseChart
  rw [â†SetCoe.ext_iff, Grassmannian.mk_apply]
  simp only
  unfold InverseChartLift
  rw [Set.range_comp, Submodule.span_image]
  apply congrArg
  rw [LinearMap.graph_eq_range_prod]
  rw [Set.range_comp, Submodule.span_image, LinearMap.range_eq_map]
  apply congrArg
  rw [Basis.span_eq]



/- We prove that the charts are inverses.-/

lemma InverseChartChart_aux1 (Ï† : E â‰ƒâ‚—[ð•œ] (Fin r â†’ ð•œ) Ã— U) {x : Grassmannian ð•œ E r}
(hx : x âˆˆ Goodset ((LinearMap.fst ð•œ (Fin r â†’ ð•œ) U).comp Ï†.toLinearMap)) {u : E} (hu : u âˆˆ x.1) :
(Chart Ï† x) (Ï† u).1 = (Ï† u).2 := by
  unfold Chart
  simp only [hx, dite_true, LinearMap.coe_comp, LinearEquiv.coe_coe, Submodule.coeSubtype, Function.comp_apply,
    LinearMap.snd_apply]
  have heq : (Ï† u).1 = ((LinearMap.fst ð•œ _ _).comp Ï†.toLinearMap).domRestrict x.1 âŸ¨u, huâŸ© := by
    simp only [LinearMap.domRestrict_apply, LinearMap.coe_comp, LinearEquiv.coe_coe, Function.comp_apply,
      LinearMap.fst_apply]
  unfold ChartAux 
  simp only [hx, dite_true, LinearMap.domRestrict_apply, LinearMap.coe_comp, LinearEquiv.coe_coe, Function.comp_apply,
    LinearMap.fst_apply]
  rw [â†LinearEquiv.invFun_eq_symm]
  rw [heq]
  erw [LinearEquiv.left_inv]
 

lemma InverseChartChart_aux2 (Ï† : E â‰ƒâ‚—[ð•œ] (Fin r â†’ ð•œ) Ã— U) {x : Grassmannian ð•œ E r}
(hx : x âˆˆ Goodset ((LinearMap.fst ð•œ (Fin r â†’ ð•œ) U).comp Ï†.toLinearMap)) :
Submodule.map Ï† x.1 = LinearMap.graph (Chart Ï† x) := by
  letI := LinearEquiv.finiteDimensional (LinearMap.graph_equiv_fst (Chart Ï† x)).symm
  apply FiniteDimensional.eq_of_le_of_finrank_eq
  . intro u 
    simp only [Submodule.mem_map, LinearMap.mem_graph_iff, forall_exists_index, and_imp] 
    intro v hvx hvu 
    rw [â†hvu]
    apply Eq.symm
    exact InverseChartChart_aux1 Ï† hx hvx
  . erw [LinearEquiv.finrank_map_eq Ï†]
    rw [LinearEquiv.finrank_eq (LinearMap.graph_equiv_fst (Chart Ï† x)), x.2.2]
    simp only [FiniteDimensional.finrank_fintype_fun_eq_card, Fintype.card_fin]


lemma InverseChartChart (Ï† : E â‰ƒâ‚—[ð•œ] (Fin r â†’ ð•œ) Ã— U) {x : Grassmannian ð•œ E r} 
(hx : x âˆˆ Goodset ((LinearMap.fst ð•œ (Fin r â†’ ð•œ) U).comp Ï†.toLinearMap)) :
InverseChart Ï† (Chart Ï† x) = x := by
  unfold InverseChart 
  simp_rw [â†(InverseChartChart_aux2 Ï† hx)]
  ext u
  simp only [Submodule.mem_map, exists_exists_and_eq_and, LinearEquiv.symm_apply_apply, exists_eq_right]
   

lemma ChartInverseChart_aux (Ï† : E â‰ƒâ‚—[ð•œ] (Fin r â†’ ð•œ) Ã— U) (f : (Fin r â†’ ð•œ) â†’â‚—[ð•œ] U) {u : E}
(hu : u âˆˆ Ï†.symm '' (LinearMap.graph f)) :
(Ï† u).2 = f (Ï† u).1 := by
  rw [LinearEquiv.image_symm_eq_preimage] at hu
  simp only [Set.mem_preimage, SetLike.mem_coe, LinearMap.mem_graph_iff] at hu 
  exact hu

lemma ChartInverseChart (Ï† : E â‰ƒâ‚—[ð•œ] (Fin r â†’ ð•œ) Ã— U) (f : (Fin r â†’ ð•œ) â†’â‚—[ð•œ] U) :
Chart Ï† (InverseChart Ï† f) = f := by
  unfold Chart ChartAux 
  simp only [InverseChart_codomainGoodset, dite_true]
  apply LinearMap.ext 
  intro v 
  simp only [LinearMap.coe_comp, LinearEquiv.coe_coe, Submodule.coeSubtype, Function.comp_apply, LinearMap.snd_apply]
  rw [ChartInverseChart_aux Ï† f]
  . apply congrArg
    change LinearMap.fst ð•œ _ _ _ = v 
    erw [â†(LinearMap.comp_apply (f := LinearMap.fst ð•œ _ _) (g := Ï†.toLinearMap))]
    have hf := InverseChart_codomainGoodset Ï† f
    rw [Goodset_iff_equiv] at hf  
    erw [â†(LinearMap.comp_apply (f := LinearMap.comp (LinearMap.fst ð•œ _ _) Ï†.toLinearMap) (x := v)
      (g := LinearMap.comp (Submodule.subtype _) (LinearEquiv.symm (LinearEquiv.ofBijective _ hf)).toLinearMap))]
    conv => rhs 
            rw [â†(LinearMap.id_apply (R := ð•œ) (M := Fin r â†’ ð•œ) v)]
    apply congrFun
    rw [â†LinearMap.comp_assoc]
    change 
      â†‘(LinearMap.comp (LinearMap.domRestrict (LinearMap.comp (LinearMap.fst ð•œ _ _) Ï†.toLinearMap) (InverseChart Ï† f).1) 
      (LinearEquiv.symm (LinearEquiv.ofBijective _ hf)).toLinearMap) = fun v => LinearMap.id v 
    simp only [LinearMap.coe_comp, LinearEquiv.coe_coe, LinearMap.id_coe, id_eq] 
    rw [â†LinearEquiv.invFun_eq_symm]
    have heq : (LinearMap.domRestrict (LinearMap.comp (LinearMap.fst ð•œ (Fin r â†’ ð•œ) U) Ï†.toLinearMap) (InverseChart Ï† f).1) =
      (LinearEquiv.ofBijective _ hf).toLinearMap := by
      ext u
      simp only [LinearMap.domRestrict_apply, LinearMap.coe_comp, LinearEquiv.coe_coe, Function.comp_apply,
        LinearMap.fst_apply, LinearEquiv.ofBijective_apply]
    nth_rewrite 1 [heq]
    erw [â†LinearEquiv.toFun_eq_coe]
    ext v 
    simp only [AddHom.toFun_eq_coe, LinearMap.coe_toAddHom, LinearEquiv.coe_coe, LinearEquiv.invFun_eq_symm,
      Function.comp_apply, LinearEquiv.apply_symm_apply]   
  . change _ âˆˆ Submodule.map Ï†.symm (LinearMap.graph f)
    unfold InverseChart
    simp only [SetLike.coe_mem]

/- Definition of the chart as LocalEquiv.-/

def Chart_LocalEquiv (Ï† : E â‰ƒâ‚—[ð•œ] (Fin r â†’ ð•œ) Ã— U) : LocalEquiv (Grassmannian ð•œ E r) ((Fin r â†’ ð•œ) â†’â‚—[ð•œ] U) :=
{
  toFun := Chart Ï†
  invFun := InverseChart Ï† 
  source := Goodset ((LinearMap.fst ð•œ (Fin r â†’ ð•œ) U).comp Ï†.toLinearMap)
  target := âŠ¤
  map_source' := by tauto 
  map_target' := fun f _ => InverseChart_codomainGoodset Ï† f
  left_inv' := fun _ hW  => InverseChartChart Ï† hW  
  right_inv' := fun f _ => ChartInverseChart Ï† f   
}

end Grassmannian
end NoTopology

